import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
	id "java"
	id "maven-publish"
	id "dev.arbjerg.lavalink.gradle-plugin" version "1.0.8"
	id "org.jetbrains.kotlin.jvm" version "1.9.0"
	id "org.jetbrains.kotlin.plugin.serialization" version "1.9.0"
}

group "com.github.topi314.sponsorblock"

def (versionStr, isSnapshot) = getGitVersion()
allprojects {
	version versionStr
}
println "Version: " + versionStr

archivesBaseName = "sponsorblock-plugin"
lavalinkPlugin {
	name = "sponsorblock-plugin"
	apiVersion = "4.0.0-beta.1"
	serverVersion = gitHash("1c0795bf156fe6559c9c0aed0412bcd8f323a3e0")
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = "UTF-8"

allprojects {
	repositories {
		mavenCentral()
		jcenter()
	}
}

var isMavenDefined = findProperty("MAVEN_USERNAME") != null && findProperty("MAVEN_PASSWORD") != null

dependencies {
	implementation(projects.protocol)
	implementation("com.github.walkyst.lavaplayer-fork:lava-common:17c75f51f9")
}
publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifactId archivesBaseName
		}
	}
}

allprojects {
	publishing {
		publications {
			withType(MavenPublication).configureEach {
				artifactId(artifactId.toLowerCase())
			}
		}
		if (isMavenDefined) {
			System.out.println("Publishing to Maven Repo")
			repositories {
				def snapshots = "https://maven.topi.wtf/snapshots"
				def releases = "https://maven.topi.wtf/releases"

				maven {
					name = "Reposilite"
					url = isSnapshot ? snapshots : releases
					credentials {
						username = findProperty("MAVEN_USERNAME")
						password = findProperty("MAVEN_PASSWORD")
					}
				}
			}
		}
	}
}

def getGitVersion() {
	def versionStr = new ByteArrayOutputStream()
	def result = exec {
		standardOutput versionStr
		errorOutput versionStr
		ignoreExitValue true
		commandLine "git", "describe", "--exact-match", "--tags"
	}
	if (result.exitValue == 0) {
		return [versionStr.toString().trim(), false]
	}


	versionStr = new ByteArrayOutputStream()
	exec {
		standardOutput versionStr
		errorOutput versionStr
		commandLine "git", "rev-parse", "--short", "HEAD"
	}

	return [versionStr.toString().trim(), true]
}

kotlin {
	compilerOptions {
		jvmTarget = JvmTarget.JVM_17
	}
}

sourceSets {
	main {
		resources {
			srcDir("build/generated/lavalink/main/resources")
		}
	}
}
